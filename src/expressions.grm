expression
  : "(" expression ")" // see tupleLiteral
  : literalConstant
  : functionLiteral
  : tupleLiteral
  : listLiteral
  : mapLiteral
  : "null"
  : "this"
  : memberAccessExpression
  : expressionWithPrecedences
  : match
  : if
  : "new" constructorInvocation // TODO: Do we need "new"?, see factory methods
  : objectLiteral
  : jump
  ;

expressionWithPrecedences // See the precedence table, everything associates to the left
  : infixFunctionCall
  : binOpExpression
  : assignment
  : unOpExpression
  : castExpression
  : typingExpression
  ;

typingExpression
  : expression typingOperation type
  ;

typingOperation
  : "as" : "is" : "isnot"
  ;

binOpExpression
  : expression binaryOperation expression // see priorities
  ;

unOpExpression
  : expression postfixUnaryOperation
  : prefixUnaryOperation expression
  ;

binaryOperation // Decreasing precedence
  // .
  // unary
  : "*" : "/" : "%"
  : "+" : "-"
  // No << >> >>>
  // named functions in infix form
  : "<" : ">" : ">=" : "<=" : "in" // is isnot as // TODO: Check the precedence for in carefully
  : "!=" : "==" : "===" : "!=="
  // No | & ^ ~
  : "&&"
  : "||"
  // assignments
  ;

assignmentOperator
  : "="
  : "+=" : "-=" : "*=" : "/=" // TODO: |=, %= and <<= make more sense than |, % or << alone, and so for others
  ;

assignment
  : memberAccessExpression assignmentOperator expression // Assignment to functions prohibited by a semantic check
  ;

prefixUnaryOperation
  : "-" : "+"
  : "++" : "--"
  : "!"  // No ~
  ;

postfixUnaryOperation
  : "++" : "--"
  ;

functionCall
  : SimpleName typeArguments valueArguments? functionLiteral?
  : SimpleName valueArguments functionLiteral?
  ;

typeArguments
  : "<" type{","} ">"
  ;

valueArguments
  : "(" expression{","} ")"
  ;

infixFunctionCall
  : expression SimpleName typeArguments expression?
  : expression SimpleName expression
  ;

jump
  : "throw" expression
  : "return" expression
  : "continue" SimpleName
  : "break" SimpleName
// yield ?
  ;

tupleLiteral // Ambiguity when after a SimpleName (infix call). In this case (e) is treated as an expression in parentheses
             // to put a tuple, write write ((e))
  : "(" expression{","} ")"
  ;

functionLiteral
  : "{" SimpleName "=>" expression "}"
  : "{" functionTypeContents "=>" expression "}"
  : "{" "(" parameter{","} ")" "=>" expression "}"
  ;

constructorInvocation
  : userType "(" expression{","} ")"
  ;

listLiteral
  : "[" expression{","} "]"
  ;

mapLiteral
  : "[" mapEntryLiteral ("," mapEntryLiteral)* "]"
  : "[" ":" "]"
  ;

mapEntryLiteral
  : expression ":" expression
  ;

literalConstant
  : booleanLiteral
  : IntegerLiteral
  : HexadecimalLiteral
  : CharacterLiteral
  : SingleLineStringLiteral
  : SingleLineStringTemplate
  : MultiLineStringLiteral
  : MultiLineStringTemplate
  ;

memberAccessExpression
  : (expression ".")? memberAccess
  ;

memberAccess
  : functionCall
  : fieldOrPropertyAccess
  : functionLiteral // no parameters allowed
  ;

fieldOrPropertyAccess
  : (FieldName | SimpleName) arrayAccess?
  ;

arrayAccess
  : "[" expression "]"
  ;

objectLiteral
  : "object" delegationSpecifier{","} classBody?
  ;

/* Factory methods:

objectLiteral
  : "object" delegationSpecifier{","} ("{" objectLiteralMember{","} "}")?
  ;

objectLiteralMember
  : memberDeclaration
  : factoryMethod
  ;

factoryMethod
  : accessModifier? SimpleName typeParameters? functionParameters functionBody
  ;

*/